import "pkg:/source/view/NodeClass.bs"
import "pkg:/source/view/Animation.bs"
import "pkg:/source/core/Utils.bs"
import "pkg:/source/view/transitions/BaseTransition.bs"

namespace mv.transitions
  ' /**
  '  * @module BaseTransition
  '  * @description BaseTransition component. extend this to create other transitions
  '  */
  @node("mv_FadeTransition", "Group")
  class FadeTransition extends mv.transitions.BaseTransition

    public color as string

    public size = [1920, 1080]

    public duration = 2

    private fadeRectangle
    private blackFadeAnim


    function new()
      super("mv_FadeTransition")
    end function

    protected override function begin()
      if m.fadeRectangle = invalid
        m.fadeRectangle = mc.createSGNode("mv_Rectangle", m.owner, "fadeRectangle", {
          "opacity": 0
          "color": m.top.color
          "size": m.top.size
        })
        m.blackFadeAnim = mv.animations.utils.fadeTo(m.fadeRectangle, m.duration, 1, false)
        m.blackFadeAnim.setKeys([0, 0.5, 1.0], [0.0, 1.0, 0.0])
      end if

      m.blackFadeAnim.onFraction(0.5, function(f as float)
        if m.source <> invalid and not m.source.isSameNode(m.target)
          m.source@._setShown(false)
          m.owner.removeChild(m.source)
        end if
        if m.target <> invalid and not m.target.isSameNode(m.source)
          m.owner.appendChild(m.target)
          m.owner.appendChild(m.fadeRectangle)

          m.target@._setShown(m.owner.isShown)
        end if
      end function, m)

      m.blackFadeAnim.observeFraction(function(f as float)
        m.top.progress = f
      end function, m)

      m.blackFadeAnim.onFraction(1, function(f as float)
        m.finish()
      end function, m)

      m.fadeRectangle.size = m.top.size
      m.fadeRectangle.color = m.top.color
      m.fadeRectangle.opacity = 0
      m.owner.appendChild(m.fadeRectangle)

      m.blackFadeAnim.start()
    end function

  end class
end namespace
